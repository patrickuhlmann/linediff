plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.0"
    id "com.github.spotbugs" version "4.2.4"
    id 'pmd'
    id 'info.solidsoft.pitest' version '1.5.1'
    id 'checkstyle'
    id "net.davidecavestro.gradle.jxr" version "0.2.1"
}

group 'ch.uhlme'
version '2.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.6.2')
    testImplementation('org.hamcrest:hamcrest-library:2.2')
    implementation('com.google.flogger:flogger:0.5.1')
    implementation('com.google.flogger:flogger-system-backend:0.5.1')
    implementation('com.github.spotbugs:spotbugs-annotations:4.0.2')
}

java {
    withJavadocJar()
    withSourcesJar()
}
jar.dependsOn javadocJar
jar.dependsOn sourcesJar

compileJava   {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'ch.uhlme.Application'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

/* jacoco configuration */
check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
jacocoTestReport.mustRunAfter test

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "patrickuhlmann_linedifference"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
    }
}

spotbugs {
    //noinspection GrFinalVariableAccess
    ignoreFailures = false
}

pmd {
    ignoreFailures = false
}

spotbugsMain {
    reports {
        html.enabled = true
    }
}

spotbugsTest {
    reports {
        html.enabled = true
    }
}

pitest {
    //adds dependency to org.pitest:pitest-junit5-plugin and sets "testPlugin" to "junit5"
    junit5PluginVersion = '0.12'
    avoidCallsTo = [ 'com.google.common.flogger',
                     'java.io.PrintStream',
                     'java.io.BufferedReader',
                     'java.io.InputStream',
                     'java.util.logging',
                     'ch.uhlme.utils.LogUtils']
}

checkstyle {
    toolVersion '8.32'
    maxWarnings = 0
}